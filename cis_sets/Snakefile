subworkflow cis:
    workdir: "../cis_analysis"
    snakefile: "../cis_analysis/Snakefile"

subworkflow ploidy_compare:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

rule all:
    input:
        "poisson_set.pdf",
        expand("ext_{set}.pdf", set=['set', 'set_derived'])

rule ext_assocs:
    input:
        rscript = "ext_{set}.r",
        meta = ploidy_compare("analysis_set.RData"),
        cis_gene = cis("poisson.RData"),
        cis_set = "poisson_set.RData",
        ext_gene = cis("ext_gene.RData")
    output:
        result = "ext_{set}.RData",
        plot = "ext_{set}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --meta {input.meta}"
            " --cis_gene {input.cis_gene}"
            " --cis_set {input.cis_set}"
            " --ext_gene {input.ext_gene}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule poisson_set:
    input:
        rscript = "poisson_set.r",
        gene = cis("poisson.RData")
    output:
        result = "poisson_set.RData",
        plot = "poisson_set.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.gene}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"
