configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

subworkflow network:
    workdir: "../data/networks"
    snakefile: "../data/networks/Snakefile"

subworkflow gsva:
    workdir: "../expr_sets"
    snakefile: "../expr_sets/Snakefile"

expr = {
    'mile' : '../expr_diff/eset_MILE.RData',
    'mad2pb' : '../data/rnaseq/assemble.RData'
}

rule all:
    input:
        expand("{rscript}-{kind}_{expr}.pdf", rscript=['diffcor', 'genenet'],
                kind=['genes', 'sets', 'both', 'tfs'], expr=expr.keys()),
        expand("viper-{expr}.pdf", expr=expr.keys())

rule genenet:
    input:
        rscript = "{rscript}.r",
        expr = gsva("{kind}_{expr}.RData")
    output:
        plot = "{rscript}-{kind}_{expr}.pdf"
    resources:
        mem = 10240,
        walltime = 240
    shell:
        "Rscript {input.rscript}"
            " --expr {input.expr}"
            " --plotfile {output.plot}"

rule viper:
    input:
        rscript = "viper.r",
        expr = lambda wc: expr[wc.expr],
        net = network("E-GEOD-13159.RData")
    output:
        outfile = "viper-{expr}.RData",
        plotfile = "viper-{expr}.pdf"
    resources:
        mem = 10240,
        walltime = 240
    shell:
        "Rscript {input.rscript}"
            " --expr {input.expr}"
            " --network {input.net}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"
