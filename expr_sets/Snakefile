configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

dset = {
    'mile' : '../data/arrayexpress/E-GEOD-13159.RData',
    'mile2' : '../expr_diff/eset_MILE.RData',
    'mad2pb' : '../data/rnaseq/assemble.RData'
}

dset_org = {
    'mile' : 'human',
    'mile2' : 'human',
    'mad2pb' : 'mouse'
}

rule all:
    input:
        expand("gsva_{dset}/{set}.RData", dset=dset.keys(), set=config['genesets']),
        expand("hl_{dset}.RData", dset=dset.keys())

rule highlight:
    input:
        rscript = "highlight.r",
        highlight = "highlight.yaml",
        sets = expand("gsva_{{dset}}/{set}.RData", set=config['genesets'])
    output:
        outfile = "hl_{dset}.RData"
    shell:
        "Rscript {input.rscript}"
            " --highlight {input.highlight}"
            " --outfile {output.outfile}"
            " {input.sets}"

rule gsva_set:
    input:
        rscript = "gsva_{dset}.r",
        geneset = lambda wc: genesets(dset_org[wc.dset] + "/" + wc.set + ".RData"),
        dset = lambda wc: dset[wc.dset]
    output:
        scores = "gsva_{dset}/{set}.RData"
    resources:
        mem = 10240,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --geneset {input.geneset}"
            " --outfile {output.scores}"
