configfile: "../config.yaml"

subworkflow expr:
    workdir: "../expr_diff"
    snakefile: "../expr_diff/Snakefile"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

dset = {
    'mile' : 'eset_MILE.RData',
    'mad2pb' : 'eset_Mad2PB.RData'
}

dset_org = {
    'mile' : 'human',
    'mad2pb' : 'mouse'
}

rule all:
    input:
        expand("gsva_{dset}/{set}.RData", dset=dset.keys(), set=config['genesets']),
        expand("hl_{dset}.RData", dset=dset.keys()),
        "highlight_overlap.pdf"

rule highlight:
    input:
        rscript = "highlight.r",
        highlight = "highlight.yaml",
        sets = expand("gsva_{{dset}}/{set}.RData", set=config['genesets'])
    output:
        outfile = "hl_{dset}.RData"
    shell:
        "Rscript {input.rscript}"
            " --highlight {input.highlight}"
            " --outfile {output.outfile}"
            " {input.sets}"

rule set_overlap:
    input:
        rscript = "highlight_overlap.r",
        highlight = "highlight.yaml",
        genesets = genesets([genesets("mouse/" + x + ".RData") for x in config['genesets']])
    output:
        plot = "highlight_overlap.pdf"
    shell:
        "Rscript {input.rscript}"
            " --highlight {input.select}"
            " --plotfile {output.plot}"
            " {input.genesets}"

rule gsva_set:
    input:
        rscript = "gsva_{dset}.r",
        geneset = lambda wc: genesets(dset_org[wc.dset] + "/" + wc.set + ".RData"),
        dset = lambda wc: expr(dset[wc.dset])
    output:
        scores = "gsva_{dset}/{set}.RData"
    resources:
        mem = 10240,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --dset {input.dset}"
            " --geneset {input.geneset}"
            " --outfile {output.scores}"
