configfile: "../config.yaml"

subworkflow rnaseq:
    workdir: "../data/rnaseq"
    snakefile: "../data/rnaseq/Snakefile"

subworkflow ploidy:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

subworkflow cis:
    workdir: "../cis_analysis"
    snakefile: "../cis_analysis/Snakefile"

subworkflow sets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

ins = ['Erg', 'Ets1', 'Arid2', 'Drg1', 'Crebbp', 'Xrcc6', 'Trp53',
       'Ikzf1', 'Wrap53', 'Pten', 'Suz12', 'Notch1', 'Rapgef6', 'Nf1']

rule all:
    input:
        "de.pdf",
        expand("de-{sets}.pdf", sets=config['genesets']),
        expand("ins/{ins}.pdf", ins=ins)

rule de_sets:
    input:
        rscript = "de_sets.r",
        de = "de.RData",
        sets = sets("{set}.RData")
    output:
        plot = "de-{set}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --de {input.de}"
            " --sets {input.sets}"
            " --plotfile {output.plot}"

rule de:
    input:
        rscript = "de.r",
        copies = ploidy("gene_copies.RData"),
        meta = ploidy("analysis_set.RData"),
        cis = cis("poisson.RData")
    output:
        result = "de.RData",
        plot = "de.pdf"
    resources:
        mem = 8000,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --copies {input.copies}"
            " --meta {input.meta}"
            " --cis {input.cis}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule de_ins:
    input:
        rscript = "ins.r",
        diff_expr = "de.RData"
    output:
        result = "ins/{ins}.RData",
        plot = "ins/{ins}.pdf"
    resources:
        mem = 5000,
        walltime = 60
    shell:
        "Rscript {input.rscript}"
            " --ins {wildcards.ins}"
            " --diff_expr {input.diff_expr}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"
