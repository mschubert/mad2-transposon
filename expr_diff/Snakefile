configfile: "../config.yaml"

subworkflow rnaseq:
    workdir: "../data/rnaseq"
    snakefile: "../data/rnaseq/Snakefile"

subworkflow ploidy:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

subworkflow cis:
    workdir: "../cis_analysis"
    snakefile: "../cis_analysis/Snakefile"

subworkflow sets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

subworkflow net:
    workdir: "../data/networks"
    snakefile: "../data/networks/Snakefile"

rule all:
    input:
        expand("de_{eset}.pdf", eset=['Mad2PB', 'Mad2PB+EtsErg']) # 'MILE'
#        "de.pdf", "de_Ets+Erg.pdf",
#        expand("de_sets/{sets}.pdf", sets=config['genesets']),
#        expand("{ins_type}_plots/{ins}.pdf", ins=config['ins_plots'],
#                ins_type=['ins', 'ins+aneup'])

rule diff_expr:
    input:
        rscript = "de_{eset}.r",
        eset = "eset_{eset}.RData",
        cis = cis("poisson.RData")
    output:
        result = "de_{eset}.RData",
        plot = "de_{eset}.pdf"
    resources:
        mem = 8000,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --eset {input.eset}"
            " --cis {input.cis}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"


rule eset_Mad2PBEtsErg:
    input:
        rscript = "eset_Mad2PB+EtsErg.r",
        eset = "eset_Mad2PB.RData"
    output:
        result = "eset_Mad2PB+EtsErg.RData",
        plot = "eset_Mad2PB+EtsErg.pdf"
    shell:
        "Rscript {input.rscript}"
            " --eset {input.eset}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule eset_Mad2PB:
    input:
        rscript = "eset_Mad2PB.r",
        copies = ploidy("gene_copies.RData"),
        meta = ploidy("analysis_set.RData")
    output:
        result = "eset_Mad2PB.RData",
        plot = "eset_Mad2PB.pdf"
    shell:
        "Rscript {input.rscript}"
            " --copies {input.copies}"
            " --meta {input.meta}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule eset_MILE:
    input:
        rscript = "eset_MILE.r"
    output:
        result = "eset_MILE.RData",
        plot = "eset_MILE.pdf"
    shell:
        "Rscript {input.rscript}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule de_ins:
    input:
        rscript = "{ins_type}.r",
        diff_expr = "de.RData",
        net = net("E-GEOD-13159.RData"),
        sets = sets(expand("{sets}.RData", sets=config['genesets']))
    output:
        result = "{ins_type}/{ins}.RData",
        plot = "{ins_type}_plots/{ins}.pdf"
    resources:
        mem = 5000,
        walltime = 120
    shell:
        "Rscript {input.rscript}"
            " --ins {wildcards.ins}"
            " --diff_expr {input.diff_expr}"
            " --network {input.net}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"
            " {input.sets}"
