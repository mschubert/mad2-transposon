subworkflow cis:
    workdir: "../data/cis"
    snakefile: "../data/cis/Snakefile"

subworkflow imfusion:
    workdir: "../data/rnaseq_imfusion"
    snakefile: "../data/rnaseq_imfusion/Snakefile"

subworkflow tis2:
    workdir: "../tis2_assoc-tryout"
    snakefile: "../tis2_assoc-tryout/Snakefile"

genes = ['Erg', 'Ets1', 'Pten', 'Nanos3', 'Rapgef6', 'Rps3']

rule all:
    input:
        "cis_tiles.pdf",
        expand("{gene}.pdf", gene=genes)

rule hit_table:
    input:
        rscript = "cis_sanger.r",
        dset = cis("cis_per_tumor.RData")
    output:
        table = "cis_sanger.tsv"
    shell:
        "Rscript {input.rscript} --infile {input.dset} --outfile {output.table}"

rule cis:
    input:
        table = "cis_sanger.tsv",
        reference = imfusion("STAR_GRCm38_ens92_PB")
    output:
        assocs = "cis_sanger_results.tsv"
    shell:
        "imfusion-ctg"
            " --insertions {input.table}"
            " --reference {input.reference}"
            " --output {output.assocs}"

rule plot_cis:
    input:
        rscript = "cis_tiles.r",
        ins_dna = "cis_sanger.tsv",
        ins_rna = imfusion("insertions.txt"),
        assocs_dna = "cis_sanger_results.tsv",
        assocs_rna = imfusion("merged_ctgs.txt"),
        exons = imfusion("exon_counts.txt"),
        aneuploidy = tis2("dset.RData")
    output:
        plot = "cis_tiles.pdf"
    shell:
        "Rscript {input.rscript}"
            " --ins_dna {input.ins_dna}"
            " --ins_rna {input.ins_rna}"
            " --assocs_dna {input.assocs_dna}"
            " --assocs_rna {input.assocs_rna}"
            " --exons {input.exons}"
            " --aneuploidy {input.aneuploidy}"
            " --plotfile {output.plot}"

rule plot_insert:
    input:
        rscript = "plot_fusions.r",
        ctgs = imfusion("merged_ctgs.txt"),
        insertions = imfusion("insertions.txt")
    output:
        plot = "{gene}.pdf"
    resources:
        mem = 10000
    shell:
        "Rscript {input.rscript}"
            " --ctgs {input.ctgs}"
            " --insertions {input.insertions}"
            " --gene {wildcards.gene}"
            " --plotfile {output.plot}"
