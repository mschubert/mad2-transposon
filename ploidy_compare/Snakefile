subworkflow ploidy_wgs:
    workdir: "../ploidy_from_wgs"
    snakefile: "../ploidy_from_wgs/Snakefile"

subworkflow ploidy_rnaseq:
    workdir: "../ploidy_from_rnaseq"
    snakefile: "../ploidy_from_rnaseq/Snakefile"

subworkflow meta:
    workdir: "../data/meta"
    snakefile: "../data/meta/Snakefile"

rule all:
    input:
        "compare_ploidy.pdf",
        "karyograms.pdf",
        "analysis_set.RData",
        "gene_copies.RData"

rule gene_copies:
    input:
        rscript = "gene_copies.r",
        dna = ploidy_wgs("copy_segments.RData"),
        fractions = "wgs_merge.tsv"
    output:
        mat = "gene_copies.RData"
    shell:
        "Rscript {input.rscript}"
            " --dna {input.dna}"
            " --fractions {input.fractions}"
            " --outfile {output.mat}"

rule analysis_set:
    input:
        rscript = "analysis_set.r",
        ploidy = "compare_ploidy.tsv"
    output:
        table = "analysis_set.RData"
    shell:
        "Rscript {input.rscript} --infile {input.ploidy} --outfile {output.table}"

rule compare_ploidy:
    input:
        rscript = "compare_ploidy.r",
        wgs = ploidy_wgs("copy_segments.RData"),
        eT = ploidy_rnaseq("eT_ploidy.RData"),
        merge = "wgs_merge.tsv"
    output:
        plot = "compare_ploidy.pdf",
        aneups = "compare_ploidy.tsv"
    shell:
        "Rscript {input.rscript}"

rule karyograms:
    input:
        rscript = "karyograms.r",
        wgs = ploidy_wgs("copy_segments.RData"),
        euploid = ploidy_rnaseq("eT_ploidy.RData"),
        meta = meta("meta.RData"),
        mixcr = "../data/rnaseq/mixcr_Mad2+PB.tsv" #ploidy_rnaseq("mixcr_Mad2+PB.tsv")
    output:
        plot = "karyograms.pdf"
    shell:
        "Rscript {input.rscript}"
            " --euploid {input.euploid}"
            " --dna {input.wgs}"
            " --meta {input.meta}"
            " --plotfile {output.plot}"
