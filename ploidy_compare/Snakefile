subworkflow rnaseq:
    workdir: "../data/rnaseq"

subworkflow ploidy_wgs:
    workdir: "../data/wgs"

subworkflow ploidy_rnaseq:
    workdir: "../ploidy_from_rnaseq"

subworkflow meta:
    workdir: "../data/meta"

rule all:
    input:
        "analysis_set.rds",
        "karyograms.pdf",
        "gene_copies.rds"

rule gene_copies:
    input:
        rscript = "gene_copies.r",
        dna = ploidy_wgs("30cellseq.rds"),
        fractions = "analysis_set_merge.tsv"
    output:
        mat = "gene_copies.rds"
    resources:
        mem = 4096
    shell:
        "Rscript {input.rscript}"
            " --dna {input.dna}"
            " --fractions {input.fractions}"
            " --outfile {output.mat}"

rule analysis_set:
    input:
        rscript = "analysis_set.r",
        meta = meta("meta.rds"),
        dna_seq = ploidy_wgs("30cellseq.rds"),
        rna_seq = ploidy_rnaseq("eT_ploidy.rds"),
        sc_seq = ploidy_wgs("sc_merge.rds"),
        merge = "analysis_set_merge.tsv"
    output:
        table = "analysis_set.rds",
        plot = "analysis_set.pdf"
    shell:
        "Rscript {input.rscript}"
            " --meta {input.meta}"
            " --dna_seq {input.dna_seq}"
            " --rna_seq {input.rna_seq}"
            " --sc_seq {input.sc_seq}"
            " --merge {input.merge}"
            " --outfile {output.table}"
            " --plotfile {output.plot}"

rule karyograms:
    input:
        rscript = "karyograms.r",
        expr = rnaseq("assemble.rds"),
        dna = ploidy_wgs("30cellseq.rds"),
        rna = ploidy_rnaseq("eT_ploidy.rds"),
        meta = "analysis_set.rds",
        mixcr = "../data/rnaseq/mixcr_Mad2+PB.tsv"
    output:
        plot = "karyograms.pdf"
    shell:
        "Rscript {input.rscript}"
            " --expr {input.expr}"
            " --dna {input.dna}"
            " --rna {input.rna}"
            " --meta {input.meta}"
            " --mixcr {input.mixcr}"
            " --plotfile {output.plot}"
