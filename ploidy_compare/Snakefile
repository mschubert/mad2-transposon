subworkflow ploidy_wgs:
    workdir: "../data/wgs"
    snakefile: "../data/wgs/Snakefile"

subworkflow ploidy_rnaseq:
    workdir: "../ploidy_from_rnaseq"
    snakefile: "../ploidy_from_rnaseq/Snakefile"

subworkflow meta:
    workdir: "../data/meta"
    snakefile: "../data/meta/Snakefile"

rule all:
    input:
        "analysis_set.RData",
        "karyograms.pdf",
        "gene_copies.RData"

rule gene_copies:
    input:
        rscript = "gene_copies.r",
        dna = ploidy_wgs("30cellseq.RData"),
        fractions = "analysis_set_merge.tsv"
    output:
        mat = "gene_copies.RData"
    resources:
        mem = 4096
    shell:
        "Rscript {input.rscript}"
            " --dna {input.dna}"
            " --fractions {input.fractions}"
            " --outfile {output.mat}"

rule analysis_set:
    input:
        rscript = "analysis_set.r",
        meta = meta("meta.tsv"),
        dna_seq = ploidy_wgs("30cellseq.RData"),
        rna_seq = ploidy_rnaseq("eT_ploidy.RData"),
        merge = "analysis_set_merge.tsv"
    output:
        table = "analysis_set.RData",
        plot = "analysis_set.pdf"
    shell:
        "Rscript {input.rscript}"
            " --meta {input.meta}"
            " --dna_seq {input.dna_seq}"
            " --rna_seq {input.rna_seq}"
            " --merge {input.merge}"
            " --outfile {output.table}"
            " --plotfile {output.plot}"

rule karyograms:
    input:
        rscript = "karyograms.r",
        wgs = ploidy_wgs("30cellseq.RData"),
        euploid = ploidy_rnaseq("eT_ploidy.RData"),
        meta = meta("meta.tsv"),
        mixcr = "../data/rnaseq/mixcr_Mad2+PB.tsv" #ploidy_rnaseq("mixcr_Mad2+PB.tsv")
    output:
        plot = "karyograms.pdf"
    shell:
        "Rscript {input.rscript}"
            " --euploid {input.euploid}"
            " --dna {input.wgs}"
            " --meta {input.meta}"
            " --plotfile {output.plot}"
