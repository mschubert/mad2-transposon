subworkflow ploidy_compare:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

subworkflow data_cis:
    workdir: "../data/cis"
    snakefile: "../data/cis/Snakefile"

models = ['logit', 'logit-adjNins', 'betareg', 'betareg_1clonal', 'betareg_adjNreads', 'betareg_1adjNreads',
          'lm', 'lm_1clonal', 'gamma', 'gamma_adjNins', 'gamma2', 'gamma3', 'ks']
types = ['hits', 'hits_cancer', 'near', 'near_cancer']

rule all:
    input:
        expand("{model}-{type}.pdf", model=models, type=types),
        "glmnet.pdf"

rule dset:
    input:
        rscript = "dset_merge.r",
        ploidy = ploidy_compare("compare_ploidy.tsv"),
        cis = data_cis("cis_per_tumor.RData")
    output:
        dset = "dset.RData"
    shell:
        "Rscript {input.rscript}"

rule models:
    input:
        rscript = "{model}.r",
        dset = "dset.RData"
    output:
        fits = "{model}.RData"
    resources:
        walltime = 600
    shell:
        "Rscript {input.rscript}"
            " --samples 5" # gene w/ at least 5 inserts to consider
            " --outfile {output.fits}"

rule plot:
    input:
        rscript = "plot.r",
        assocs = "{model}.RData"
    output:
        plot = "{model}-{type}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --assocs {input.assocs}"
            " --subset {wildcards.type}"
            " --plotfile {output.plot}"

rule glmnet:
    input:
        rscript = "glmnet.r"
    output:
        dset = "glmnet.RData",
        plot = "glmnet.pdf"
    shell:
        "Rscript {input.rscript}"
            " --samples 5"
            " --outfile {output.dset}"
            " --plotfile {output.plot}"
