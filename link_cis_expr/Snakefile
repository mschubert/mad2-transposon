configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

expr = {
    'mile' : '../data/arrayexpress/E-GEOD-13159.RData',
    'mad2pb' : '../data/rnaseq/assemble.RData'
}

rule all:
    input:
        "gene_overlap.pdf",
        expand("cor_{expr}.pdf", expr=expr.keys())

rule set_overlap:
    input:
        rscript = "gene_overlap.r",
        select = "interesting_sets.yaml",
        genesets = [genesets(x + ".RData") for x in config['genesets']]
    output:
        plot = "gene_overlap.pdf"
    shell:
        "Rscript {input.rscript}"
            " --select {input.select}"
            " --plotfile {output.plot}"
            " {input.genesets}"

rule gsva:
    input:
        rscript = "set_gsva.r",
        geneset = genesets("{set}.RData"),
        expr = lambda wc: expr[wc.expr]
    output:
        scores = "gsva_{expr}/{set}.RData"
    shell:
        "Rscript {input.rscript}"
            " --expr {input.expr}"
            " --geneset {input.geneset}"
            " --outfile {output.scores}"

rule cor_net:
    input:
        rscript = "set_cor.r",
        genesets = expand("gsva_{{expr}}/{sets}.RData", sets=config['genesets'])
    output:
        plot = "cor_{expr}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --plotfile {output.plot}"
            " {input.genesets}"
