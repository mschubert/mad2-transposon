configfile: "../config.yaml"

subworkflow genesets:
    workdir: "../data/genesets"
    snakefile: "../data/genesets/Snakefile"

subworkflow network:
    workdir: "../data/networks"
    snakefile: "../data/networks/Snakefile"

subworkflow gsva:
    workdir: "../expr_sets"
    snakefile: "../expr_sets/Snakefile"

expr = {
    'mile' : '../data/arrayexpress/E-GEOD-13159.RData',
    'mile2' : '../expr_diff/eset_MILE.RData',
    'mad2pb' : '../data/rnaseq/assemble.RData'
}

rule all:
    input:
        "gene_overlap.pdf",
        expand("genenet_{expr}.pdf", expr=expr.keys()),
        expand("viper_{expr}.pdf", expr=expr.keys())

rule set_overlap:
    input:
        rscript = "gene_overlap.r",
        select = "interesting_sets.yaml",
        genesets = genesets([genesets("mouse/" + x + ".RData") for x in config['genesets']])
    output:
        plot = "gene_overlap.pdf"
    shell:
        "Rscript {input.rscript}"
            " --select {input.select}"
            " --plotfile {output.plot}"
            " {input.genesets}"

rule cor_genenet:
    input:
        rscript = "cor_genenet.r",
        select = "interesting_sets.yaml",
        expr = lambda wc: expr[wc.expr],
        genesets = expand("../expr_sets/gsva_{{expr}}/{sets}.RData", sets=config['genesets'])
    output:
        plot = "genenet_{expr}.pdf"
    resources:
        mem = 10240,
        walltime = 240
    shell:
        "Rscript {input.rscript}"
            " --select {input.select}"
            " --expr {input.expr}"
            " --plotfile {output.plot}"
            " {input.genesets}"

rule cor_viper:
    input:
        rscript = "cor_viper.r",
        expr = lambda wc: expr[wc.expr],
        net = network("E-GEOD-13159.RData")
    output:
        plot = "viper_{expr}.pdf"
    resources:
        mem = 10240,
        walltime = 240
    shell:
        "Rscript {input.rscript}"
            " --expr {input.expr}"
            " --network {input.net}"
            " --plotfile {output.plot}"
