configfile: "../../config.yaml"

subworkflow aex:
    workdir: "../arrayexpress"
    snakefile: "../arrayexpress/Snakefile"

subworkflow gsets:
    workdir: "../genesets"
    snakefile: "../genesets/Snakefile"

rule all:
    input:
        "E-GEOD-13159.RData",
        expand("set_fet/{sets}.RData", sets=config['genesets'])

rule aracne:
    input:
        rscript = "aracne.r",
        expr = aex("{accession}.RData")
    output:
        data = "{accession}.RData"
    resources:
        mem = 10240,
        walltime = 720
    wildcard_constraints:
        accession = "^E.*"
    shell:
        "Rscript {input.rscript} --infile {input.expr} --outfile {output.data}"

rule set_fet:
    input:
        rscript = "set_fet.r",
        net = "E-GEOD-13159.RData",
        sets = gsets("human/{sets}.RData")
    output:
        data = "set_fet/{sets}.RData"
    resources:
        walltime = 240
    shell:
        "Rscript {input.rscript}"
            " --network {input.net}"
            " --setfile {input.sets}"
            " --outfile {output.data}"
