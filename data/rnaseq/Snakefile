import yaml
import os.path

def load_yaml(fname):
    sset = yaml.load(open(fname))
    sset['samples'] = { k:[os.path.join(sset['directory'], vi) for vi in v]
            for k,v in sset['samples'].items() }
    return sset

ssets = ['Mad2+PB_batch1', 'Mad2+PB_batch2', 'Mad2+PB_batch3',
         'Mad2+p53_batch1', 'Mad2+p53_batch2']
samples = { sset:load_yaml(sset+".yaml") for sset in ssets }

wildcard_constraints:
    sset = ".*batch.*"

rule all:
    input:
        "assemble.RData",
        expand("{sset}.RData", sset=ssets[0:3]),
        expand("mixcr_{sset}.tsv", sset=ssets)

rule assemble_rnaseq:
    input:
        rscript = "assemble.r",
        expr = expand("{sset}.RData", sset=ssets[0:3]) #TODO: meta for qc
    output:
        merged = "assemble.RData",
        plot = "assemble.pdf"
    shell:
        "Rscript {input.rscript}"
            " --outfile {output.merged}"
            " --plotfile {output.plot}"

rule qc_rnaseq:
    input:
        rscript = "qc_rnaseq.r",
        counts = "{sset}.tsv",
        stats = "{sset}.tsv.summary"
    output:
        expr = "{sset}.RData",
        plot = "{sset}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.counts}"
            " --stats {input.stats}"
            " --outfile {output.expr}"
            " --plotfile {output.plot}"

rule star_genome:
    input:
        fasta = "../genome/Mus_musculus.GRCm38.dna.primary_assembly.fa",
        gtf = "../genome/Mus_musculus.GRCm38.92.gtf"
    output:
        genome = "STAR_GRCm38_ens92"
    resources:
        mem = 100000,
        walltime = 240
    shell:
        "mkdir {output.genome};"
        "STAR --runMode genomeGenerate"
            " --genomeFastaFiles {input.fasta}"
            " --sjdbGTFfile {input.gtf}"
            " --limitGenomeGenerateRAM {resources.mem}000000"
            " --genomeDir {output.genome}"

rule align:
    input:
        genome = "STAR_GRCm38_ens92",
        fastq = lambda wc: samples[wc.set]['samples'][wc.sample]
    output:
        bam = "aligned/{set}/{sample}.bam"
    resources:
        mem = 30000,
        walltime = 120
    shell:
        "STAR --runMode alignReads"
            " --genomeLoad LoadAndKeep"
            " --readFilesCommand zcat"
            " --genomeDir {input.genome}"
            " --readFilesIn {input.fastq}"
            " --outSAMtype BAM Unsorted"
            " --outStd BAM_Unsorted"
            " --outFileNamePrefix aligned/{wildcards.set}/{wildcards.sample}"
            " > {output.bam}"

rule count:
    input:
        gtf = "../genome/Mus_musculus.GRCm38.92.gtf",
        samples = "{sset}.yaml",
        bams = lambda wc: expand("aligned/{sset}/{samples}.bam",
                sset=wc.sset, samples=samples[wc.sset]['samples'].keys())
    output:
        counts = "{sset}.tsv"
    resources:
        mem = 2000,
        walltime = 120
    shell:
        "featureCounts"
            " -a {input.gtf}"
            " -o {output.counts}"
            " {input.bams}"

rule mixcr:
    """
    http://mixcr.readthedocs.io/en/latest/rnaseq.html
    """
    input:
        fastq = lambda wc: samples[wc.set]['samples'][wc.sample]
    output:
        vdjca = "mixcr/{set}/{sample}.vdjca",
        align_log = "mixcr/{set}/{sample}_align.log",
        contigs1 = "mixcr/{set}/{sample}_contigs1.vdjca",
        contigs2 = "mixcr/{set}/{sample}_contigs2.vdjca",
        assemble_log = "mixcr/{set}/{sample}_assemble.log",
        clones = "mixcr/{set}/{sample}_clones.clns",
        outfile = "mixcr/{set}/{sample}.txt"
    resources:
        mem = 20000,
        walltime = 240
    shell:
        """
        mixcr align --species mmu --parameters rna-seq --threads 1 \
            -OallowPartialAlignments=true \
            --report {output.align_log} {input.fastq} {output.vdjca}
        mixcr assemblePartial {output.vdjca} {output.contigs1}
        mixcr assemblePartial {output.contigs1} {output.contigs2}
        mixcr assemble --threads 1 --report {output.assemble_log} \
            {output.contigs2} {output.clones}
        mixcr exportClones {output.clones} {output.outfile}
        """

rule mixcr_combine:
    input:
        rscript = "mixcr.r",
        samples = "{sset}.yaml",
        mixcr = lambda wc: expand("mixcr/{sset}/{sample}.txt",
                sset=wc.sset, sample=samples[wc.sset]['samples'].keys())
    output:
        tsv = "mixcr_{sset}.tsv"
    shell:
        "Rscript {input.rscript} -o {output.tsv} {input.mixcr}"
