configfile: "../../config.yaml"

subworkflow mi_network:
    workdir: "../networks"

subworkflow expr_stat1:
    workdir: "../../expr_stat1"

enrichr_sets = set(config['genesets']) - set("MILE_regulons")
org = ['human', 'mouse']

rule all:
    input:
        expand("{org}/{geneset}.rds", org=org, geneset=enrichr_sets),
        expand("{org}/MILE_regulons.rds", org=org),
        expand("{org}/stat1_ko.rds", org=org)

rule enrichr:
    input:
        rscript = "make_set.r"
    output:
        human = "human/{geneset}.rds",
        mouse = "mouse/{geneset}.rds"
    shell:
        "Rscript {input.rscript}"
            " --geneset {wildcards.geneset}"
            " --human {output.human}"
            " --mouse {output.mouse}"

rule mi_regulons:
    input:
        rscript = "mi_regulons.r",
        network = mi_network("aracne_E-GEOD-13159.rds")
    output:
        human = "human/MILE_regulons.rds",
        mouse = "mouse/MILE_regulons.rds"
    shell:
        "Rscript {input.rscript}"
            " --network {input.network}"
            " --human {output.human}"
            " --mouse {output.mouse}"

rule stat1_ko:
    input:
        rscript = "stat1_ko.r",
        diff_expr = expr_stat1("diff_expr.rds")
    output:
        human = "human/stat1_ko.rds",
        mouse = "mouse/stat1_ko.rds"
    shell:
        "Rscript {input.rscript}"
            " --diff_expr {input.diff_expr}"
            " --human {output.human}"
            " --mouse {output.mouse}"
