configfile: "../config.yaml"

subworkflow cis:
    workdir: "../data/cis"
    snakefile: "../data/cis/Snakefile"

subworkflow aneup:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

subworkflow imfusion:
    workdir: "../data/rnaseq_imfusion"
    snakefile: "../data/rnaseq_imfusion/Snakefile"

rule all:
    input:
        "plot_tiles.pdf", "poisson.pdf", "ext_gene.pdf",
        expand("bionet_{int}.pdf", int=['DLBCL', 'omnipath']),
        expand("ins/{ins}.pdf", ins=config['ins_plots'])

rule plot_ins:
    input:
        rscript = "plot_ins.r",
        exons = imfusion("exon_counts.txt"),
        ctgs = imfusion("merged_ctgs.txt"),
        rna_ins = imfusion("insertions.txt"),
        dna_ins = "analysis_set.RData",
        meta = aneup("analysis_set.RData")
    output:
        plot = "ins/{gene}.pdf"
    resources:
        mem = 10000
    shell:
        "Rscript {input.rscript}"
            " --exons {input.exons}"
            " --ctgs {input.ctgs}"
            " --rna_ins {input.rna_ins}"
            " --dna_ins {input.dna_ins}"
            " --meta {input.meta}"
            " --gene {wildcards.gene}"
            " --plotfile {output.plot}"

rule plot_tiles:
    input:
        rscript = "plot_tiles.r",
        aneup = "../ploidy_compare/analysis_set.RData",
        ins_dna = "poisson.RData",
        ins_rna = imfusion("insertions.txt"),
        assocs_dna = "ext_gene.RData",
        assocs_rna = imfusion("merged_ctgs.txt"),
        exons = imfusion("exon_counts.txt")
    output:
        plot = "plot_tiles.pdf"
    shell:
        "Rscript {input.rscript}"
            " --aneup {input.aneup}"
            " --ins_dna {input.ins_dna}"
            " --ins_rna {input.ins_rna}"
            " --assocs_dna {input.assocs_dna}"
            " --assocs_rna {input.assocs_rna}"
            " --exons {input.exons}"
            " --plotfile {output.plot}"

rule bionet:
    input:
        rscript = "bionet.r",
        cis = "poisson.RData",
        aneup = "ext_gene.RData"
    output:
        outfile = "bionet_{interactome}.RData",
        plotfile = "bionet_{interactome}.pdf"
    shell:
        "Rscript {input.rscript}"
            " --cis {input.cis}"
            " --aneup {input.aneup}"
            " --interactome {wildcards.interactome}"
            " --outfile {output.outfile}"
            " --plotfile {output.plotfile}"

rule ext_assocs:
    input:
        rscript = "ext_gene.r",
        poisson = "poisson.RData",
        meta = aneup("analysis_set.RData")
    output:
        result = "ext_gene.RData",
        plot = "ext_gene.pdf"
    shell:
        "Rscript {input.rscript}"
            " --meta {input.meta}"
            " --poisson {input.poisson}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule poisson_gene:
    input:
        rscript = "poisson.r",
        aset = "analysis_set.RData"
    output:
        result = "poisson.RData",
        plot = "poisson.pdf"
    shell:
        "Rscript {input.rscript}"
            " --infile {input.aset}"
            " --outfile {output.result}"
            " --plotfile {output.plot}"

rule cimpl:
    input:
        rscript = "kernel_cimpl.r",
        aset = "analysis_set.RData"
    output:
        result = "kernel_cimpl.RData"
    shell:
        "Rscript {input.rscript} --infile {input.aset} --outfile {output.result}"

rule analysis_set:
    input:
        rscript = "analysis_set.r",
        sheet = "analysis_set.yaml",
        data = cis("cis_per_tumor.RData")
    output:
        aset = "analysis_set.RData"
    shell:
        "Rscript {input.rscript} --infile {input.data} --sheet {input.sheet} --outfile {output.aset}"
