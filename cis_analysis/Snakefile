subworkflow cis:
    workdir: "../data/cis"
    snakefile: "../data/cis/Snakefile"

subworkflow aneup:
    workdir: "../ploidy_compare"
    snakefile: "../ploidy_compare/Snakefile"

rule all:
    input:
        "poisson.RData",
        "kernel_cimpl.RData",
        "aneuploidy_assocs.RData",
        "aneup_de.pdf"

rule aneuploidy_assocs:
    input:
        rscript = "aneuploidy_assocs.r",
        poisson = "poisson.RData",
        aneup = aneup("analysis_set.RData")
    output:
        result = "aneuploidy_assocs.RData"
    shell:
        "Rscript {input.rscript}"
            " --aneup {input.aneup}"
            " --poisson {input.poisson}"
            " --outfile {output.result}"

rule aneup_de:
    input:
        rscript = "aneup_de.r",
        poisson = "poisson.RData",
        expr = "../data/rnaseq/assemble.RData",
        aneup = aneup("analysis_set.RData")
    output:
        data = "aneup_de.RData",
        plot = "aneup_de.pdf"
    shell:
        "Rscript {input.rscript}"
            " --cis {input.poisson}"
            " --expr {input.expr}"
            " --aneup {input.aneup}"
            " --outfile {output.data}"
            " --plotfile {output.plot}"

rule poisson: # keep separate for now, bc gene set analysis
    input:
        rscript = "poisson.r",
        aset = "analysis_set.RData"
    output:
        result = "poisson.RData"
    shell:
        "Rscript {input.rscript} --infile {input.aset} --outfile {output.result}"

rule cimpl:
    input:
        rscript = "kernel_cimpl.r",
        aset = "analysis_set.RData"
    output:
        result = "kernel_cimpl.RData"
    shell:
        "Rscript {input.rscript} --infile {input.aset} --outfile {output.result}"

rule anaylsis_set:
    input:
        rscript = "analysis_set.r",
        data = cis("cis_per_tumor.RData")
    output:
        aset = "analysis_set.RData"
    shell:
        "Rscript {input.rscript} --infile {input.data} --outfile {output.aset}"
